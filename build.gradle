plugins {
    id 'me.champeau.gradle.jmh' version '0.2.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'me.champeau.gradle.jmh'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.pinterest'
version = '1.0.1-SNAPSHOT'

ext.jmhVer      = '1.11.3'
ext.hdrHistVer  = '2.1.8'
ext.quasarVer   = '0.7.4'
ext.comsatVer   = '0.7.0'
ext.httpCoreVer = '4.4.4'
ext.slf4jVer    = '1.7.13'
ext.junitVer    = '4.12'

repositories {
    // mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

configurations {
    quasar
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()

        force "org.hdrhistogram:HdrHistogram:$hdrHistVer"
        force "org.apache.httpcomponents:httpcore:$httpCoreVer"
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: "$junitVer"
    compile group: 'co.paralleluniverse', name: 'quasar-core', version: "$quasarVer", classifier: 'jdk8'
    compile group: 'co.paralleluniverse', name: 'comsat-httpclient', version: "$comsatVer"
    compile group: 'org.hdrhistogram', name: 'HdrHistogram', version: "$hdrHistVer"
    compile group: 'org.slf4j', name: 'slf4j-api', version: "$slf4jVer"
    // compile group: 'org.slf4j', name: 'slf4j-simple', version: "$slf4jVer"

    // For the IDE
    // compile "org.openjdk.jmh:jmh-core:$jmhVer"
    // compile "org.openjdk.jmh:jmh-generator-annprocess:$jmhVer"

    quasar group: 'co.paralleluniverse', name: 'quasar-core', version: "$quasarVer", classifier: 'jdk8'
}

jmh {
    jmhVersion = "$jmhVer"
    include = '.*'
    jvmArgs = "-server -XX:+TieredCompilation -XX:+AggressiveOpts -javaagent:${configurations.quasar.iterator().next()} -Dco.paralleluniverse.fibers.detectRunawayFibers=false"
    benchmarkMode = 'avgt' // 'thrpt'
    timeUnit = 'ms'
}

tasks.withType(Test) {
    allJvmArgs = []
    useJUnit()

    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}"
}
